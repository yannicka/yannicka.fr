namespace weapon.projectile {
	class Rocket : Projectile {
		def new(x double, y double, angle double, speed int) {
			super(x, y, 17, 5, angle, speed)

			loadImage(Img.get("rocketLauncherBullet"), width, height)

			explosion = Explosion.new(center.x, center.y, 40, 50)

			position += velocity * 0.032

			# Les dommages de la roquette proviennent de l'explosion
			damages = 0
		}

		over update(dt double) {
			super(dt)

			# Met à jour la position de l'explosion
			explosion.position = center

			for i in 0 .. Math.irand(1, 2) {
				var p = Particle.new
					.position(center)
					.speed(20, 40)
					.life(0.4, 0.6)
					.radius(1, 3)
					.angle(0, 360)
					.color([
						rgb(255, 140, 0),
						rgb(200, 100, 0),
						rgb(160, 60, 0)
					])

				Particle.particles.append(p)
			}
		}

		def explode {
			# Déclenche l'explosion
			explosion.explode(self)
		}

		over onCollision(b block.Block) {
            # Pas de collision sur le bonus de vie
			if b is block.bonus.IncreaseHealth {
				return
			}

			# Explose la roquette
			explode

			# Supprime la roquette
			kill
		}

		over onPlayerOverlaps(player Player) {
            # Baisse la vie du joueur
            player.decreaseHealth(damages)

			# Explose la roquette
			explode

			# Supprime la roquette
			kill
		}
	}
}
